import flixel.animation.FlxAnimation;
import flixel.util.FlxBaseSignal;

class VSliceCharacter extends Character {
    /**
     * This character plays a given animation when hitting these specific combo numbers.
     */
    public var comboNoteCounts(default, null):Array<Int> = [];

    /**
     * This character plays a given animation when dropping combos larger than these numbers.
     */
    public var dropNoteCounts(default, null):Array<Int> = [];

    public var onPlayAnim:FlxBaseSignal<(animName:String, forced:Bool, context:Dynamic, reversed:Bool, frame:Int)->Void> = new FlxBaseSignal();

    public function new(x:Float, y:Float, ?character:String, ?isPlayer:Bool, ?switchAnims:Bool) {
		isPlayer ??= false;
		switchAnims ??= true;
        super(x, y, character, isPlayer, switchAnims, false);

        this.comboNoteCounts = getCountAnims("combo");
        this.dropNoteCounts = getCountAnims("drop");
    }

    private function getCountAnims(prefix:String):Array<Int> {
        var result:Array<Int> = [];
        var anims:Array<String> = this.animation.getNameList();
    
        for (anim in anims) {
            if (StringTools.startsWith(anim, prefix)) {
                var comboNum:Int = Std.parseInt(anim.substring(prefix.length));
                if (comboNum != null) {
                    result.push(comboNum);
                }
            }
        }

        // sort numerically
        result.sort((a, b) -> a - b);
        return result;
    }

    private function playComboAnim(comboCount:Int) {
        var animName:String = "combo" + Std.string(comboCount);
        if (hasAnim(animName)) {
            playAnim(animName, true);
        }
    }

    private function playDropAnim(comboCount:Int) {
        var animName:Null<String> = null;

        // Chooses the combo drop animation to play.
        // If they're several animations, the highest one will be played.
        for (dropCount in dropNoteCounts) {
            if (comboCount >= dropCount) {
                animName = "drop" + Std.string(dropCount);
            }
        }

        if (animName != null && hasAnim(animName)) {
            playAnim(animName, true);
        }
    }

    override public function playAnim(AnimName:String, ?Force:Bool, ?Context:PlayAnimContext, ?Reversed:Bool, ?Frame:Int) {
        Force ??= false;
		Context ??= "NONE";
        Reversed ??= false;
        Frame ??= 0;
        super.playAnim(AnimName, Force, Context, Reversed, Frame);
        dispatchSignal(onPlayAnim, AnimName, Force, Context, Reversed, Frame);
    }
}